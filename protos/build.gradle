plugins {
    id 'java'
    id "com.google.protobuf" version "0.9.5"
    id 'maven-publish'
}

group = 'com.chilicizz.protos'
version = '0.1'
def grpcVersion = '1.77.0'
def protobufVersion = '3.25.5'
def protocVersion = protobufVersion

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation("javax.annotation:javax.annotation-api:1.3.2")
    implementation('com.google.protobuf:protobuf-java:4.33.2')
    implementation('com.google.api.grpc:proto-google-common-protos:2.63.2')
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.grpc:grpc-stub:${grpcVersion}")
    testImplementation("io.grpc:grpc-testing:${grpcVersion}")
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
        all().configureEach { task ->
            task.builtins {
            }
        }
    }
}

// https://docs.github.com/en/packages/working-with-a-github-packages-registry/working-with-the-gradle-registry#publishing-a-package
// https://docs.github.com/en/actions/tutorials/publish-packages/publish-java-packages-with-gradle#publishing-packages-to-github-packages
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/chilicizz/vanguard")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}