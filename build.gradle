plugins {
    id("com.gradleup.shadow") version "9.3.1"
    // https://micronaut-projects.github.io/micronaut-gradle-plugin/latest/
    id("io.micronaut.application") version "4.6.1"
    // https://gradle-ssh-plugin.github.io/
    id('org.hidetake.ssh') version '2.12.0'
}

version = "0.1"
group = "com.cyrilng"

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(':protos'))
    implementation(project(':rss'))
    // https://guides.micronaut.io/latest/micronaut-static-resources-gradle-java.html
    implementation("io.micronaut.views:micronaut-views-thymeleaf")
    implementation("io.micronaut:micronaut-http-server-netty")

    annotationProcessor('io.micronaut.serde:micronaut-serde-processor')
    implementation('io.micronaut.serde:micronaut-serde-jackson')
    implementation('io.micronaut.grpc:micronaut-grpc-runtime')

    implementation('javax.annotation:javax.annotation-api:1.3.2')
    runtimeOnly('ch.qos.logback:logback-classic:1.5.23')

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation('org.mockito:mockito-core:5.21.0')
    testImplementation('org.awaitility:awaitility:4.3.0')
    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    implementation("org.apache.commons:commons-math3:3.6.1")

    // https://www.mongodb.com/docs/languages/java/reactive-streams-driver/current/
    implementation(platform("org.mongodb:mongodb-driver-bom:5.6.2"))
    implementation('org.mongodb:mongodb-driver-reactivestreams')

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


application {
    mainClass.set("com.cyrilng.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}

micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.cyrilng.*")
    }
}


tasks.named("dockerBuildNative") {
    images = ["ghcr.io/chilicizz/vanguard:${project.version}"]
    copy {
        from "${rootProject.projectDir}/fly.toml"
        into "build/docker"
    }
}

tasks.named("dockerBuild") {
    images = ["ghcr.io/chilicizz/vanguard:${project.version}"]
    copy {
        from "${rootProject.projectDir}/fly.toml"
        into "build/docker"
    }
}

tasks.register('printVersionInformation') {
    println project.version
}

tasks.register('installOverSsh') {
    doLast {
        // Deploying over ssh
        remotes {
            pi4 {
                host = System.getenv("VG_HOST")
                user = System.getenv("VG_USER")
                identity = file(System.getenv("VG_PK_FILE"))
                knownHosts = allowAnyHosts
            }
        }
        ssh.run {
            session(remotes.pi4) {
                put from: "${layout.buildDirectory.get()}/distributions/vanguard-${version}.tar", into: '/tmp'
                execute "sudo tar xf /tmp/vanguard-${version}.tar -C /opt/vanguard --strip-components=1"
                put from: "${layout.buildDirectory.get()}/vanguard.service", into: '/tmp'
                execute "sudo cp /tmp/vanguard.service /etc/systemd/system"
                execute "sudo systemctl daemon-reload"
                execute "sudo systemctl start vanguard.service"
                execute "sudo systemctl enable vanguard.service"
                execute "sudo rm /tmp/vanguard-${version}.tar /tmp/vanguard.service"
            }
        }
    }
}

tasks.register('deployOverSsh') {
    doLast {
        // Deploying over ssh
        remotes {
            pi4 {
                host = System.getenv("VG_HOST")
                user = System.getenv("VG_USER")
                identity = file(System.getenv("VG_PK_FILE"))
                knownHosts = allowAnyHosts
            }
        }
        ssh.run {
            session(remotes.pi4) {
                put from: "${layout.buildDirectory.get()}/distributions/vanguard-${version}.tar", into: '/tmp'
                execute "sudo tar xf /tmp/vanguard-${version}.tar -C /opt/vanguard --strip-components=1"
                execute "sudo systemctl restart vanguard"
                execute "sudo rm /tmp/vanguard-${version}.tar"
            }
        }
    }
}